/*
	Practico 1 - Ejercicio NoSe:
*/

set Productos = {a, b, c, d}
set ParaPrensar = {a, c, d}
set ParaTaladrar = {b, c, d}

CINTA = (bring[p:Productos] -> stop[p] -> elevar[p] -> CINTA).

BRAZOIN = (elevar[p:Productos] -> (
  when (p == 'a || p == 'c) press.acquire[p] -> press.place[p] -> BRAZOIN |
  when (p == 'b || p == 'd) drill.acquire[p] -> drill.place[p] -> BRAZOIN)
).

BRAZOP2D = (press.extraer.c -> drill.acquire.c -> drill.place.c -> BRAZOP2D).
BRAZOD2P = (drill.extraer.d -> press.acquire.d -> press.place.d -> BRAZOD2P).

BRAZOOUT = (
  press.extraer[p:{a, d}] -> depositar[p] -> BRAZOOUT |
  drill.extraer[p:{b, c}] -> depositar[p] -> BRAZOOUT
).

PRENSA  = (acquire[p:ParaPrensar] -> place[p] -> procesar[p] -> extraer[p] -> PRENSA).
TALADRO = (acquire[p:ParaTaladrar] -> place[p] -> procesar[p] -> extraer[p] -> TALADRO).

OUT = (depositar[p:Productos] -> salida[p] -> OUT).

||CELULA = (CINTA || BRAZOIN || BRAZOP2D || BRAZOD2P || BRAZOOUT || press:PRENSA || drill:TALADRO || OUT) << {
  press.extraer.c, drill.acquire.c, drill.place.c, // Prioritizing
  drill.extraer.d, press.acquire.d, press.place.d
}.
